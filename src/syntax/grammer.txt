Program → FunctionList

FunctionList → Function FunctionList
             | ε


Function → Type id ( ParamList ) Block

int fact(int n) { return n * fact(n-1); }


ParamList → Param MoreParams
          | ε

MoreParams → , Param MoreParams
           | ε

Param → Type id


Statement → Declaration ;
          | Assignment ;
          | ReturnStmt ;
          | Expression ;
          | Block


Block → { StatementList }

StatementList → Statement StatementList
              | ε


Declaration → Type id
            | Type id [ num ]

Assignment → id = Expression
           | id [ Expression ] = Expression


ReturnStmt → return Expression


Expression → Term E'

E' → + Term E'
    | - Term E'
    | ε

Term → Factor T'

T' → * Factor T'
    | / Factor T'
    | % Factor T'
    | ε


Factor → id
       | num
       | ( Expression )
       | id ( ArgList )        // function call
       | id [ Expression ]     // array access


ArgList → Expression MoreArgs
        | ε

MoreArgs → , Expression MoreArgs
         | ε


Type → int | float | char | void
